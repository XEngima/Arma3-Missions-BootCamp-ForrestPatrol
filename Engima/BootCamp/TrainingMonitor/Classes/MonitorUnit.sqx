/*
 * Name:	MonitorUnit
 * Date:	2017-10-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class that models a unit with given feedback and a relative score.
 */
 
using Sqx.Collections;
 
namespace Engima.BootCamp.TrainingMonitors
{
	public class MonitorUnit : IComparer
	{
		// Creates a MonitorUnit object.
		public constructor {
			params ["_unit" as Object, "_feedback" as String, "_score" as Scalar];
			
			_self.Unit = _unit;
			_self.Feedback = _feedback;
			_self.Score = _score;
		};
		
		public static method AddUnitScore {
			params ["_monitorUnits" as Array, "_unit" as Object, "_feedback" as String, "_score" as Scalar];
			
			scopeName "main";
			
			{
				if (_x.Unit == _unit && _x.Feedback == _feedback) then {
					_x.Score = _x.Score + _score;
					
					if (_x.Score < -1000) then {
						_x.Score = -1000;
					};
					breakOut "main";
				};
			} foreach _monitorUnits as MonitorUnit;
			
			if (_score < -1000) then {
				_score = -1000;
			};
					
			_monitorUnits pushBack [_unit, _feedback, _score] new MonitorUnit;
		};
		
		public property Object Unit { get; private set; };
		
		public property String Feedback { get; private set; };
		
		public property Scalar Score { get; set; };
		
		public method Scalar Compare {
			params ["_otherMonitorUnit" as MonitorUnit];
			private _result = 0;
			
			if (_self.Score < _otherMonitorUnit.Score) then {
				_result = -1;
			};
			if (_self.Score > _otherMonitorUnit.Score) then {
				_result = 1;
			};
			
			_result;
		};
		
		public method ToString {
			(name _self.Unit) + ": " + _self.Feedback + ", " + (str _self.Score)
		};
	};
};
