/*
 * Name:	MissionEngine
 * Date:	2017-09-29
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class that controls the mission events.
 */
 
using Sqx.Markers;
using Engima.BootCamp.TrainingMonitor;
 
namespace Mission.Server
{
	public class MissionEngine
	{
		private fields ["_mStartPos"];
	
		// Creates a MissionEngine object.
		public constructor {			
		};
		
		private method MonitorPlayerAliveStatus {
			waitUntil { { _x getVariable ["ENG_BOOTCAMP_isNeutralized", false] } count units group (allPlayers select 0) == count units group (allPlayers select 0) };
			ENG_missionSuccess = false;
			publicVariable "ENG_missionSuccess";
		};
		
		public method Start {
			private ["_memberDistanceMonitor" as MemberDistanceMonitor, "_lifeStateMonitor" as LifeStateMonitor, "_stealthMonitor" as StealthMonitor];
			
			[] spawn _self.MonitorPlayerAliveStatus;
			
			waitUntil { count allPlayers > 0 };
			
			//_mStartPos = getPos (allPlayers select 0); TODO: Kompilerar fel - ett semikolon fÃ¶r mycket!
			private _firstPlayer = (allPlayers select 0);
			_mStartPos = getPos _firstPlayer;
			
			waitUntil { { _mStartPos distance _x > 75 } count allPlayers > 0 };
			
			["Mission has started. Enemy patrol is on the move!"] call dre_fnc_CL_ShowTitleTextAllClients;
			
			waitUntil { !isNil "eng_enemyLeader" };
			
			_memberDistanceMonitor = [group (allPlayers select 0), 5, 30] new MemberDistanceMonitor;
			_lifeStateMonitor = [group (allPlayers select 0)] new LifeStateMonitor;
			_stealthMonitor = [units group eng_enemyLeader, units group (allPlayers select 0)] new StealthMonitor;
			
			call _memberDistanceMonitor.Start;
			call _lifeStateMonitor.Start;
			call _stealthMonitor.Start;
			
			[] spawn {
				private ["_marker" as Marker];
				_marker = classNull;
				
				while { true } do {
					waitUntil { sleep (random 60); { eng_enemyLeader distance _x < 400 } count allPlayers > 0 };
					
					if (isNull _marker) then {
						_marker = [getPos eng_enemyLeader, "o_inf", "ColorOpfor"] call Marker.CreateIconMarker as Marker;
					}
					else {
						[getPos eng_enemyLeader] call _marker.SetPosition;
					};
					
					["Enemy patrol sighted in direction " + str (round ((leader group player) getDir getPos eng_enemyLeader)) + ". Moving in direction " + str (round getDir eng_enemyLeader) + "."] call dre_fnc_CL_ShowTitleTextAllClients;
					
					waitUntil { sleep 5; { eng_enemyLeader distance _x > 400 } count allPlayers == count allPlayers };
				};
			};
			
			// Wait until either all enemies are dead, or all players are dead
			waitUntil { sleep 3; !isNil "ENG_missionSuccess" };
			
			if (ENG_missionSuccess) then {
				["TakeOutEnemyPatrol", "SUCCEEDED"] call ENGTASKS_SetTaskState;
			}
			else {
				["TakeOutEnemyPatrol", "FAILED"] call ENGTASKS_SetTaskState;
			};
			
			call _memberDistanceMonitor.Stop;
			call _lifeStateMonitor.Stop;
			call _stealthMonitor.Stop;
			
			ENG_memberDistanceMonitor = _memberDistanceMonitor;
			ENG_lifeStateMonitor = _lifeStateMonitor;
			ENG_stealthMonitor = _stealthMonitor;
			
			publicVariable "ENG_memberDistanceMonitor";
			publicVariable "ENG_lifeStateMonitor";
			publicVariable "ENG_stealthMonitor";
		};
	};
};
